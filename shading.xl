// Shading module definition
//
// This module allows to apply some shading effects
//
// Copyright 2010-2011 Taodyne

module_description
    id "c09af2a2-448a-4844-85f3-ab06073f29b4"
    name "Shading"
    import_name "Shading"
    description "A module that allows to add some shading effects to a scene"
    author "Taodyne SAS"
    website "http://www.taodyne.com"
    version "1.0"

module_description "fr",
    name "Ombrage"
    description "Permet d'ajouter des effets d'ombrages non-photoréalistes dans une scène."

// -------------------------------------------------------------------------------------------------------------------
//   Cel shading (or Toon shading)
//   For further informations, see http://en.wikipedia.org/wiki/Cel-shaded_animation
// -------------------------------------------------------------------------------------------------------------------
//Define cell-shading color
CELL_COLOR_R -> 1.0
CELL_COLOR_R := 1.0
CELL_COLOR_G -> 1.0
CELL_COLOR_G := 1.0
CELL_COLOR_B -> 1.0
CELL_COLOR_B := 1.0

cel_color r:real, g:real, b:real ->
    /**
    * Define main color of cell shading.
    **/
    CELL_COLOR_R := r
    CELL_COLOR_G := g
    CELL_COLOR_B := b

cel_shading->
    /**
    * Apply cell shading on current scene.
    * It change current color in a toon one define by cell_color.
    **/
    shader_program
        shader_log
        vertex_shader <<#extension GL_EXT_gpu_shader4 : require
            #extension GL_EXT_gpu_shader4 : enable

            uniform vec3 cel_color;
            uniform int  lights;
            varying vec3 color;
            void main()
            {
                gl_Position = ftransform();

                vec3 normal         = normalize(gl_NormalMatrix * gl_Normal);
                vec3 eyeCoordinates = normalize((gl_ModelViewMatrix * gl_Vertex).xyz);

                float diff = 0.0;
                float spec = 0.0;

                if(lights == 0)
                {
                    vec3 lightDir       = normalize(gl_LightSource[0].position.xyz - eyeCoordinates);
                    vec3 reflectVec     = normalize(reflect( -lightDir, normal ));

                    //Diffuse intensity
                    diff = max(dot(normal,lightDir), 0.0);

                    //Specular intensity
                    if (diff > 0.0)
                       spec = max(dot(reflectVec, -eyeCoordinates), 0.0);

                    diff = diff * 0.6 + spec * 0.4;
                }
                else
                {
                    for(int i = 0; i < lights; i++)
                    {
                        if(bool(lights & (1 << i)))
                        {
                            vec3 lightDir       = normalize(gl_LightSource[i].position.xyz - eyeCoordinates);
                            vec3 reflectVec     = normalize(reflect( -lightDir, normal ));

                            //Diffuse intensity
                            float diffTmp = max(dot(normal,lightDir), 0.0);

                            //Specular intensity
                            if (diffTmp > 0.0)
                               spec = max(dot(reflectVec, -eyeCoordinates), 0.0);

                            diff +=  diffTmp * 0.6 + spec * 0.4;
                        }
                    }
                }

                //Compute final color
                if(diff < 0.2)
                   color = cel_color * 0.5;
                else if(diff < 0.5)
                   color = cel_color * 0.75;
                else
                   color = cel_color;
            }
            >>
        fragment_shader <<
            varying vec3 color;
            void main()
            {
                gl_FragColor = vec4(color, 1.0);
            }
            >>
    shader_set lights    := light_id
    shader_set cel_color := (CELL_COLOR_R; CELL_COLOR_G; CELL_COLOR_B)

// -------------------------------------------------------------------------------------------------------------------
//   Gooch shading
//   For further informations, see http://www.yaldex.com/open-gl/ch18lev1sec2.html
// -------------------------------------------------------------------------------------------------------------------
DIFFUSE_WARM -> 0.45
DIFFUSE_WARM := 0.45

DIFFUSE_COOL -> 0.045
DIFFUSE_COOL := 0.045

//Define warm color
WARM_COLOR_R -> 1.0
WARM_COLOR_R := 1.0
WARM_COLOR_G -> 1.0
WARM_COLOR_G := 1.0
WARM_COLOR_B -> 1.0
WARM_COLOR_B := 1.0

//Define cool color
COOL_COLOR_R -> 0.0
COOL_COLOR_R := 0.0
COOL_COLOR_G -> 0.0
COOL_COLOR_G := 0.0
COOL_COLOR_B -> 0.0
COOL_COLOR_B := 0.0

//Define surface color
SURFACE_COLOR_R -> 1.0
SURFACE_COLOR_R := 1.0
SURFACE_COLOR_G -> 1.0
SURFACE_COLOR_G := 1.0
SURFACE_COLOR_B -> 1.0
SURFACE_COLOR_B := 1.0

warm_color r:real, g:real, b:real ->
    /**
    * Define warm color for gooch shading
    **/
    WARM_COLOR_R := r
    WARM_COLOR_G := g
    WARM_COLOR_B := b

cool_color r:real, g:real, b:real ->
    /**
    * Define cool color for gooch shading
    **/
    COOL_COLOR_R := r
    COOL_COLOR_G := g
    COOL_COLOR_B := b

surface_color r:real, g:real, b:real ->
    /**
    * Define surface color for gooch shading
    **/
    SURFACE_COLOR_R := r
    SURFACE_COLOR_G := g
    SURFACE_COLOR_B := b

warm_diffuse w:real ->
    /**
    * Define warm diffuse coeff for gooch shading
    **/
    DIFFUSE_WARM := w


cool_diffuse c:real ->
    /**
    * Define cool diffuse coeff for gooch shading
    **/
    DIFFUSE_COOL := c

gooch_shading ->
    /**
    * Apply gooch shading on current scene.
    * It change current color in a toon one, result of a mix
    * between these define by surface_color, cool_color and warm_color.
    * Mixing coefficients are defined by diffuse_warm and diffuse_cool.
    **/
    shader_program
        shader_log
        vertex_shader <<#extension GL_EXT_gpu_shader4 : require
            #extension GL_EXT_gpu_shader4 : enable

           uniform float diffuse_warm;
           uniform float diffuse_cool;

           uniform vec3 warm_color;
           uniform vec3 cool_color;
           uniform vec3 surface_color;

           uniform int  lights;

           varying vec3 color;

           void main()
           {
              gl_Position = ftransform();

              vec3 normal         = normalize(gl_NormalMatrix * gl_Normal);
              vec3 eyeCoordinates = normalize((gl_ModelViewMatrix * gl_Vertex).xyz);

              float diff = 0.0;
              float spec = 0.0;

              if(lights == 0)
              {
                  vec3 lightDir       = normalize(gl_LightSource[0].position.xyz - eyeCoordinates);
                  vec3 reflectVec     = normalize(reflect( -lightDir, normal ));

                  //Diffuse intensity
                  diff = max(dot(normal,lightDir), 0.0);

                  //Specular intensity
                  if (diff > 0.0)
                  {
                     spec = max(dot(reflectVec, -eyeCoordinates), 0.0);
                     spec = pow(spec, 32.0);
                  }
              }
              else
              {
                  for(int i = 0; i < lights; i++)
                  {
                      if(bool(lights & (1 << i)))
                      {

                          vec3 lightDir       = normalize(gl_LightSource[i].position.xyz - eyeCoordinates);
                          vec3 reflectVec     = normalize(reflect( -lightDir, normal ));

                          //Diffuse intensity
                          float diffTmp = max(dot(normal,lightDir), 0.0);

                          //Specular intensity
                          if (diffTmp > 0.0)
                          {
                             float specTmp = max(dot(reflectVec, -eyeCoordinates), 0.0);
                             spec += pow(specTmp, 32.0);
                          }

                          diff += diffTmp;
                      }
                  }
              }

              //Mix colors
              vec3 kcool  = min(cool_color + diffuse_cool * surface_color, 1.0);
              vec3 kwarm  = min(warm_color + diffuse_warm * surface_color, 1.0);
              vec3 kfinal = mix(kcool, kwarm, diff);

              //Compute final color
              color = min(kfinal + spec, 1.0);
           }
           >>
        fragment_shader <<
           varying vec3 color;
           void main()
           {
               gl_FragColor = vec4(color, 1.0);
           }
           >>
    shader_set lights        := light_id
    shader_set diffuse_warm  := DIFFUSE_WARM;
    shader_set diffuse_cool  := DIFFUSE_COOL;
    shader_set warm_color    := (WARM_COLOR_R; WARM_COLOR_G; WARM_COLOR_B)
    shader_set cool_color    := (COOL_COLOR_R; COOL_COLOR_G; COOL_COLOR_B)
    shader_set surface_color := (SURFACE_COLOR_R; SURFACE_COLOR_G; SURFACE_COLOR_B)
